plugins {
    id 'maven-publish'
    id 'signing'
}

group = 'io.github.wliamp.contracts'

// ------------------------
// Publishing configuration
// ------------------------
publishing {
    publications {
        mavenContracts(MavenPublication) {
            artifactId = project.hasProperty("artifactId") ? project.property("artifactId") : "contracts-unknown"
            if (project.hasProperty("artifactFile")) {
                def artifactFile = file(project.property("artifactFile"))
                if (artifactFile.exists()) {
                    artifact(artifactFile)
                }
            }
            pom {
                name.set("Contracts")
                description.set("API contracts & events")
                url.set("https://github.com/wliamp/contracts-pps")
            }
        }
    }

    repositories {
        maven {
            name = "OSSRH"
            url = uri("https://oss.sonatype.org/service/local/staging/deploy/maven2/")
            credentials {
                username = System.getenv("OSSRH_USERNAME") ?: ""
                password = System.getenv("OSSRH_PASSWORD") ?: ""
            }
        }
    }
}

if (project.hasProperty("artifactFile")) {
    signing {
        useGpgCmd()
        sign publishing.publications.mavenContracts
    }
}
